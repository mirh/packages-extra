# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

_linuxprefix=linux54
_extramodules=extramodules-5.4-MANJARO
pkgname=$_linuxprefix-catalyst
_pkgname=catalyst
pkgver=15.302.2301
pkgrel=1
_pkgver=15.12
provides=("$_pkgname=$_pkgver")
groups=("$_linuxprefix-extramodules")
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only."
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
makedepends=("$_linuxprefix-headers")
conflicts=('catalyst-legacy' "$_linuxprefix-catalyst-legacy")
install=catalyst.install

source=(https://drivers.amd.com/drivers/firepro/15.302.2301-linux-retail_end_user.zip
        #https://launchpad.net/ubuntu/+archive/primary/+files/fglrx-installer_${_tarball}.orig.tar.gz
        ati_make.sh
        makefile_compat.patch
        lano1106_fglrx_intel_iommu.patch
        lano1106_kcl_agp_13_4.patch
        fglrx_gpl_symbol.patch
        4.3-kolasa-seq_printf.patch
        4.3-gentoo-mtrr.patch
        crimson_i686_xg.patch
        4.4-manjaro-xstate.patch
        grsec_arch.patch
        4.4-to-4.10-arch-get_user_pages-page_cache_release.patch
        4.7-arch-cpu_has_pge-v2.patch
        4.10-arch-sling00-virtual_address-acpi_get_table_with_size-clts.patch
        4.11-npfeiler-signal_vmf.patch
        4.12-npfeiler-PUD_OFFSET.patch
        4.12-npfeiler-movsl_mask.patch
        4.13-npfeiler-wait_queue_t.patch
        4.14-npfeiler-task_struct-mm_segment_t.patch
        4.14.21_4.15.5-npfeiler-flush_tlb_one_kernel.patch
        4.17-npfeiler-pci_get_domain_bus_and_slot.patch
        4.20-npfeiler-libfglrx_ip-nodkms.patch
        4.21-manjaro-npfeiler.patch
        5.1-npfeiler.patch
        5.3-smp_call_function.patch
        15.9-preempt.patch
	LICENSE.TXT)

url_ref="https://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="https::/usr/bin/curl --referer ${url_ref} -o %o %u"

sha256sums=('3acff4f15946394112311e32e9db723546ec4c4da89c1f6c4f420060c507c150'
            '8b2adf804c2691d075bb75fbb626be59f3769a2f4f1418554fc3de91a65451ab'
            'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
            'd7ab220204976b5802dbfa7cbd05ec0c5a9ff3524a2020b1b8deff4f601a05fa'
            '37344e6949b391a54491bcba68c2239393e442a00114ee36fe25d775d9656520'
            'bf9cfd7252b3a33c50087233f7270180c0f59837bcc9a82482a0367c995c89e6'
            '33b87af3b1b5ab53b32bf972949db1e845beebe1e3d7effc06d11cf8c57f3768'
            '2881026d607f242efc4569bd39d647eae4f2e402da06de5b90476a57c349777c'
            'cae7603b0b4dfb6594a7d2c7de83f7e37dd2567486a1aced5870b4611a09f422'
            '411c7aea2929cc9bb776cb1ba1df57e1d0f75ee6c84766a7fb791cb6f82446d2'
            'd3695c90d14de5a52a7f4c878ec161a0282ae169c59bbf953950aad7bfb59fdc'
            '2b83463a47e5a677352f62122fa6655f49c6b1e94b16b9d37b7476ecab2bd20d'
            '39a09293916c876681591dbf20ee09c2971a016081f8aacb90f40227b13d5677'
            'fbba904ed4f69578177f2a5d3e33d1d78069efa1c483f0c2c8b77e49e57cae88'
            '50cf50dced85d267c3a9df997219f75bef1fc19755041f83931eddafd713051b'
            '6c54da21862f11747449d9d4de3fdec1047ca9c1b7a8bc3e1d4b66098fa18ba0'
            'f613e1bc2de3c558e9c808b52f48a6803a778673a9ac009c31cd0cf3c142e5a9'
            '57db04fb9d6f4f5a434ff58c225f882531f74e3d17bc6c9c2defb075b0fb3496'
            'b3b1ccb42829e39a829d71cad2caf4628b83cc22cb18595408c35405f0e5ab6f'
            '9e86af669533a72c1507e7dbbf7e07af2a24f8fb6decb6e07a441f3d2d705a58'
            '7d3fd4aa044c8bf047246021b68d6bd89798fd44f0eedc481c7a335f57425434'
            'ce90f034e1630c4581bbc7186e8ca41c3a0b7e129510137257f0c02ffa077e03'
            'a139162276b92443651e890421887898342acf754e2a3e7aad503042474aaf17'
            '80166cbd974045d4764b48a09748974d74bedfaa7fc547c404daa48132c7efc6'
            '06ac4b9f83f2d5c137e674585d143e0aa528ed9676d38449ae9077db2c2d5be6'
            '4e2628a54e80ae9d1e325504dd8626943a6d3e2e4da8a12f0fc39edf284e7466'
            '4f7530540f6e1ea032746e94bd164160abdf93c15509bfb8bdbecd23bcea5c47')

prepare() {
	/bin/sh ./fglrx-15.302.2301/amd-driver-installer-15.302.2301-x86.x86_64.run --extract archive_files
	cd "${srcdir}/archive_files"

	## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
	## works only in combination with ati_make.sh script
        patch -Np1 -i  ${srcdir}/makefile_compat.patch
        patch -Np1 -i  ${srcdir}/lano1106_fglrx_intel_iommu.patch
        patch -Np1 -i  ${srcdir}/lano1106_kcl_agp_13_4.patch
        patch -Np1 -i  ${srcdir}/fglrx_gpl_symbol.patch
        patch -Np1 -i  ${srcdir}/4.3-kolasa-seq_printf.patch
        patch -Np1 -i  ${srcdir}/4.3-gentoo-mtrr.patch
        test "${CARCH}" = "i686" && patch -Np1 -i  ${srcdir}/crimson_i686_xg.patch
        patch -Np1 -i  ${srcdir}/4.4-manjaro-xstate.patch
        patch -Np1 -i  ${srcdir}/grsec_arch.patch
        patch -Np1 -i  ${srcdir}/4.4-to-4.10-arch-get_user_pages-page_cache_release.patch
        patch -Np1 -i  ${srcdir}/4.7-arch-cpu_has_pge-v2.patch
        patch -Np1 -i  ${srcdir}/4.10-arch-sling00-virtual_address-acpi_get_table_with_size-clts.patch
        patch -Np1 -i  ${srcdir}/4.11-npfeiler-signal_vmf.patch
        patch -Np1 -i  ${srcdir}/4.12-npfeiler-PUD_OFFSET.patch
        patch -Np1 -i  ${srcdir}/4.12-npfeiler-movsl_mask.patch
        patch -Np1 -i  ${srcdir}/4.13-npfeiler-wait_queue_t.patch
        patch -Np1 -i  ${srcdir}/4.14-npfeiler-task_struct-mm_segment_t.patch
        patch -Np1 -i  ${srcdir}/4.14.21_4.15.5-npfeiler-flush_tlb_one_kernel.patch
        patch -Np1 -i  ${srcdir}/4.17-npfeiler-pci_get_domain_bus_and_slot.patch
        patch -Np1 -i  ${srcdir}/4.20-npfeiler-libfglrx_ip-nodkms.patch
        patch -Np1 -i  ${srcdir}/4.21-manjaro-npfeiler.patch
        patch -Np1 -i  ${srcdir}/5.1-npfeiler.patch
        patch -Np1 -i  ${srcdir}/5.3-smp_call_function.patch
        patch -Np1 -i  ${srcdir}/15.9-preempt.patch
}

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	## include ati_make.sh and use _ati_check function from it
	cd "${srcdir}"
	. ati_make.sh
	_ati_check

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "${srcdir}/archive_files"

        cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" .
	cp 2.6.x/Makefile .

	## Compile module using _ati_check variables
	make -C /usr/lib/modules/${_kernver}/build M="`pwd`" ARCH=${BUILDARCH} \
	MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
	PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE EXTRA_LDFLAGS:="`pwd`"/libfglrx_ip.a modules
}


package() {
	depends=("$_linuxprefix" "catalyst-utils")

	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	## License
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"

	## Install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" "$startdir"/*.install
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" "$startdir"/*.install
}
