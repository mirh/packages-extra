# Contributor: Vi0L0 <vi0l093@gmail.com>
# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

_pkgsourcename=catalyst-utils
pkgbase=lib32-$_pkgsourcename
pkgname=("lib32-$_pkgsourcename" 'lib32-opencl-catalyst')
pkgver=15.201.1151
pkgrel=7
_pkgver=15.201.1151
pkgdesc="AMD/ATI catalyst driver utilities and libraries. (32-bit)"
url="http://www.amd.com"
arch=(x86_64)
license=('custom')
groups=('lib32')
depends=('lib32-libxext' 'lib32-libdrm' 'lib32-mesa' 'catalyst-utils' 'mhwd')
conflicts=('lib32-catalyst-legacy-utils' 'lib32-catalyst-13.4-utils')
install="${pkgname}.install"
source=(https://www2.ati.com/drivers/linux/amd-catalyst-15.9-linux-installer-15.201.1151-x86.x86_64.zip
        #https://launchpad.net/ubuntu/+archive/primary/+files/fglrx-installer_${_tarball}.orig.tar.gz
        lib32-catalyst.sh
	LICENSE.TXT)
sha256sums=('bf3e6e7d5c51db3d075410a3f116f865b82823debc1d66698d187249feec6a91'
            '1f4f611ed80e2626ef39486020a06c328c15dad76e1365a4bd553342060ff409'
            '4f7530540f6e1ea032746e94bd164160abdf93c15509bfb8bdbecd23bcea5c47')
	    
url_ref="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="https::/usr/bin/curl --referer ${url_ref} -o %o %u"

prepare() {
	#cd ${srcdir}/fglrx-$_pkgver
	/bin/sh ./AMD-Catalyst-15.9-Linux-installer-${_pkgver}-x86.x86_64.run --extract archive_files
	#/bin/sh ./amd-catalyst-${_pkgver}-linux-x86.x86_64.run --extract archive_files

	# move files
	#mkdir common
	#mv etc lib usr common
	#mkdir -p fglrx-$_pkgver/archive_files
	#mv arch common xpic xpic_64a LICENSE.TXT fglrx-$_pkgver/archive_files
	mkdir -p fglrx-$_pkgver
	mv archive_files fglrx-$_pkgver
}

package_lib32-catalyst-utils() {
      cd "${srcdir}"
      install -D -m755 lib32-catalyst.sh "${pkgdir}/etc/profile.d/lib32-catalyst.sh"
      cd "${srcdir}/fglrx-$_pkgver/archive_files/arch/x86/usr"
      install -dm755 "${pkgdir}/usr/lib32"
      install -dm755 "${pkgdir}/usr/lib32/fglrx"
      install -dm755 "${pkgdir}/usr/lib32/dri"
      install -dm755 "${pkgdir}/usr/lib32/xorg/modules/dri"
      install -m755 lib/*.so* "${pkgdir}/usr/lib32"
      install -m755 X11R6/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx/libGL.so.1.2"
      ln -sf libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx/libGL.so.1"
      ln -sf libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx/libGL.so"

      # GLX extension module for X
      install -D -m755 ../../../xpic/usr/X11R6/lib/modules/extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib32/fglrx/xorg/libglx.so.1"
      ln -s "libglx.so.1"  "${pkgdir}/usr/lib32/fglrx/xorg/libglx.so"

      # We have to provide symlink to mesa (see https://bugzilla.redhat.com/show_bug.cgi?id=1413579#c57)
      #ln -s "/usr/lib32/libGLX_mesa.so.0.0.0" "${pkgdir}/usr/lib32/libGLX_indirect.so.0"
      #ln -s "fglrx/xorg/libglx.so.1" "${pkgdir}/usr/lib32/libGLX_indirect.so.0"

      # We have to provide symlinks to mesa, as catalyst doesn't ship them
      ln -s /usr/lib32/libEGL.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libEGL.so.1.0.0"
      ln -s libEGL.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libEGL.so.1"
      ln -s libEGL.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libEGL.so"
      ln -s /usr/lib32/libGLESv1_CM.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv1_CM.so.1.0.0"
      ln -s libGLESv1_CM.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv1_CM.so.1"
      ln -s libGLESv1_CM.so.1.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv1_CM.so"
      ln -s /usr/lib32/libGLESv2.so.2.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv2.so.2.0.0"
      ln -s libGLESv2.so.2.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv2.so.2"
      ln -s libGLESv2.so.2.0.0 "${pkgdir}/usr/lib32/fglrx/libGLESv2.so"

      install -m755 X11R6/lib/libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32"
      install -m755 X11R6/lib/libatiadlxx.so "${pkgdir}/usr/lib32"
      install -m755 X11R6/lib/libfglrx_dm.so.1.0 "${pkgdir}/usr/lib32"
      install -m755 X11R6/lib/libXvBAW.so.1.0 "${pkgdir}/usr/lib32"
      install -m755 X11R6/lib/modules/dri/*.so "${pkgdir}/usr/lib32/xorg/modules/dri"
      ln -snf /usr/lib32/xorg/modules/dri/fglrx_dri.so "${pkgdir}/usr/lib32/dri/fglrx_dri.so"

      cd "$pkgdir/usr/lib32"
      ln -sf libfglrx_dm.so.1.0 libfglrx_dm.so.1
      ln -sf libAMDXvBA.so.1.0 libAMDXvBA.so.1
      ln -sf libXvBAW.so.1.0 libXvBAW.so.1
      ln -sf libatiuki.so.1.0 libatiuki.so.1
      ln -sf libatiuki.so.1.0 libatiuki.so

      # License
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
      install -m644 "${srcdir}/fglrx-$_pkgver/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"

      install -dm 755 "${pkgdir}"/etc/ld.so.conf.d
      echo -e '/usr/lib32/fglrx/' > "${pkgdir}"/etc/ld.so.conf.d/00-lib32-fglrx.conf

      # provided in lib32-opencl-catalyst package
      rm "${pkgdir}"/usr/lib32/lib{amdocl*,OpenCL}.so* 
}

package_lib32-opencl-catalyst() {
      pkgdesc="OpenCL implemention from AMD (32-bit)"
      provides=('lib32-opencl-driver')
      conflicts=('lib32-opencl-amd' 'lib32-opencl-catalyst-legacy' 'lib32-opencl-catalyst-13.4')
      depends=('lib32-gcc-libs')
      optdepends=('opencl-headers: headers necessary for OpenCL development'
                  'lib32-opencl-icd-loader: OpenCL ICD Bindings (32-bit)')
      
      install -dm755 "${pkgdir}"/usr/lib32
      install -dm755 "${pkgdir}/etc/OpenCL/vendors"
      install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
      
      _ARCHDIR="x86"
      cd "${srcdir}/fglrx-$_pkgver/archive_files/arch/${_ARCHDIR}"
      install -m644 etc/OpenCL/vendors/amdocl*.icd "${pkgdir}/etc/OpenCL/vendors/"
      install -m755 usr/lib/libamdocl*.so "${pkgdir}/usr/lib32/"
#      install -m755 usr/lib/libOpenCL.so.1 "${pkgdir}/usr/lib32/"        #lib32-opencl-icd-loader provides this
#      ln -s libOpenCL.so.1 "${pkgdir}/usr/lib32/libOpenCL.so"
      # license
      install -m644 "${srcdir}/fglrx-$_pkgver/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"
}
