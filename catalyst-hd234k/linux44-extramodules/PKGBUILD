# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

_linuxprefix=linux44
_extramodules=extramodules-4.4-MANJARO
pkgname=$_linuxprefix-catalyst-legacy
_pkgname=catalyst-legacy
pkgver=13.1
pkgrel=1
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
pkgdesc="AMD/ATI legacy Catalyst drivers for linux. fglrx kernel module only."
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
makedepends=("$_linuxprefix-headers")
conflicts=('catalyst' "$_linuxprefix-catalyst")
install=catalyst-legacy.install

source=(https://drivers.amd.com/drivers/legacy/amd-driver-installer-catalyst-13.1-legacy-linux-x86.x86_64.zip
		ati_make.sh
		makefile_compat.patch
		3.5-do_mmap.patch
		arch-fglrx-3.7.patch
		gentoo_linux-3.10-proc.diff
		foutrelis_3.10_fix_for_legacy.patch
		lano1106_fglrx_intel_iommu.patch
		lano1106_kcl_agp_13_4.patch
		cold-fglrx-3.14-current_euid.patch
		kolasa-3.19-get_cpu_var.patch
		fglrx_gpl_symbol.patch
		ubuntu_buildfix_kernel_4.0.patch
		ubuntu_buildfix_kernel_4.1.patch
		arch-fglrx-acpi_handle.patch
		4.2-ubuntu_buildfix_kernel_4.2-modified.patch
		4.2-fglrx-has_fpu.patch
		4.2-kolasa-fpu_save_init.patch
		4.3-gentoo-mtrr.patch
		gcc5-something_something_the_dark_side.patch
		4.4-arch-block_signals.patch
		4.4-to-4.10-arch-get_user_pages-hd234k.patch)

url_ref="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="https::/usr/bin/curl --referer ${url_ref} -o %o %u"

md5sums=('c07fd1332abe4c742a9a0d0e0d0a90de'
		'fd98b7e486d7fd4cad8de7b95b5b031e'
		'3e1b82bd69774ea808da69c983d6a43b'
		'a450e2e3db61994b09e9d99d95bee837'
		'ff60c162b46e21e9810a722718023451'
		'5872d95907a93ada44982e355e91e59d'
		'5d6b5ae55adfb4909e042f50400a4e2d'
		'5184b94a2a40216a67996999481dd9ee'
		'c5156eddf81c8a1719b160d05a2e8d67'
		'ba33b6ef10896d3e1b5e4cd96390b771'
		'3aa45013515b724a71bbd8e01f98ad99'
		'ef97fc080ce7e5a275fe0c372bc2a418'
		'40aaf97acae268f8f7949e0fecb926d9'
		'34f818673aec1eb2edb5f913b071ba08'
		'645422762125052a0f13ecd03d7bf9dd'
		'65ec204c8013fb5dc6aa624563c14512'
		'ae67dff6c218e028443dff6eacb26485'
		'ccfdf4784735a742c53bdc1309f49a51'
		'98828e3eeaec2b3795e584883cc1b746'
		'af80a9eb2016811ab79717c2bd370a25'
		'd200e156e941ec7b0227e399fd20a9c2'
		'f8802c1368bcb17acdf8c0977fa3b7a5')

prepare() {
	/bin/sh ./amd-driver-installer-catalyst-13.1-legacy-linux-x86.x86_64.run --extract archive_files

	cd "${srcdir}/archive_files"

	## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
	## works only in combination with ati_make.sh script
	patch -Np1 -i ../makefile_compat.patch
	patch -Np1 -i ../3.5-do_mmap.patch
	patch -Np1 -i ../arch-fglrx-3.7.patch
	patch -Np0 -i ../gentoo_linux-3.10-proc.diff
	patch -l -Np1 -i ../foutrelis_3.10_fix_for_legacy.patch
	patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
	patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
	patch -Np1 -i ../cold-fglrx-3.14-current_euid.patch
	patch -Np1 -i ../kolasa-3.19-get_cpu_var.patch
	patch -Np1 -i ../fglrx_gpl_symbol.patch
	patch -Np1 -i ../ubuntu_buildfix_kernel_4.0.patch
	patch -Np1 -i ../ubuntu_buildfix_kernel_4.1.patch
	patch -Np1 -i ../arch-fglrx-acpi_handle.patch
	patch -Np1 -i ../4.2-ubuntu_buildfix_kernel_4.2-modified.patch
	patch -Np1 -i ../4.2-fglrx-has_fpu.patch
	patch -Np1 -i ../4.2-kolasa-fpu_save_init.patch
	patch -Np1 -i ../4.3-gentoo-mtrr.patch
	patch -Np1 -i ../gcc5-something_something_the_dark_side.patch
	patch -Np1 -i ../4.4-arch-block_signals.patch
	patch -Np1 -i ../4.4-to-4.10-arch-get_user_pages-hd234k.patch
}

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	## include ati_make.sh and use _ati_check function from it
	cd "${srcdir}"
	. ati_make.sh
	_ati_check

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

        cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" .
	cp 2.6.x/Makefile .

	## Compile module using _ati_check variables
	make -C /usr/lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
	MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
	PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}


package() {
	depends=("$_linuxprefix" "catalyst-legacy-utils")

	cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	## License
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"

	## Install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" "$startdir"/*.install
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" "$startdir"/*.install
}
